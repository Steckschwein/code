!address{
	.screen=$c000
}
.screen_status=tmp2
; !byte .TEXTUI_ENABLED
.COLS=32
.WRITE=1<<0
.DIRTY=1<<1
.CURSOR=1<<2
.CURSOR_BUSY=1<<3
.TEXTUI_ENABLED=1<<4
.CURSOR_CHAR=160

init_vdp
.textui_init0
	jsr	vdp_display_off			;display off
	jsr vdp_mode_gfx1_sprites_off
	
	jsr	.textui_blank			;blank screen buffer
	
	stz	crs_x
	stz	crs_y
	stz	tmp2
	jsr .textui_update_crs_ptr		;init cursor pointer
.textui_init
	+SetVector	charset, .adrl
	
	lda	#$00
	ldy	#.WRITE_ADDRESS+$00
	ldx	#$08					;load charset
	jsr	vdp_memcpy
	lda	#Medium_Green<<4|Black		;enable gfx 1
	jmp	vdp_mode_gfx1

.textui_blank
	ldx	#$00
	lda	#' '
-	sta	.screen,x
	sta	.screen+$100,x
	sta	.screen+$200,x
	sta	.screen+$300,x
	inx
	bne	-
-	sta	.screen+$400,x	;last line for scroll up
	inx
	cpx	#.COLS
	bne	-
	jmp	.textui_screen_dirty

.textui_update_crs_ptr
	pha
	lda	crs_y
	asl	
	asl
	asl
	asl			
	asl			; offs= y*.COLS
	ora	crs_x
	sta	crs_ptr
	
	lda	crs_y	; * .COLS
	lsr			; /8 -> page offset 0-2
	lsr
	lsr
	clc
	adc	#>.screen
	sta	crs_ptr+1
	pla
	rts
	
.textui_screen_dirty
	lda	.screen_status		;set dirty
	ora	#.DIRTY
	sta	.screen_status
	rts
