*= $e000

text_mode_40 	= 1
num_ls_entries 	= $03

!src <defs.h.a>
; !src <bios.h.a>
!src <kernel.h.a>
!src <shell.h.a>
!src <fat32.h.a>
!src <errors.h.a>


KEY_RETURN 	= $0d
KEY_BACKSPACE 	= $08
KEY_ESCAPE	= $1b
KEY_ESCAPE_CRSR_UP	= 'A'
KEY_ESCAPE_CRSR_DOWN	= 'B'
BUF_SIZE	= 32

!address {
.buf 				= $e800 ; Input buffer 80 bytes. end: $d800
.endbuf				= .buf + BUF_SIZE*16
.bufptr				= $d0
.bufhwm				= $d2
; Address pointers for serial upload
.startaddr	= ptr1
entryvec			= $d4
}

;---------------------------------------------------------------------------------------------------------
; init shell
;  - init sd card (again)
;  - mount sd card
;  - print welcome message
;---------------------------------------------------------------------------------------------------------

.init	
	jsr init_textui
	
	sei
	jsr init_sdcard
	cli
	lda errno
	beq +

	+ShellPrintString .txt_msg_sd_init_error
	jmp .hello

+	
	jsr mount
	

	+SetVector mainloop, retvec
	+SetVector .buf, .bufptr

	; set attrib mask. hide volume label and hidden files
	lda #$0a
	sta dir_attrib_mask

	jmp	.hello

mainloop
	; output prompt character
	+ShellPrintString .prompt

	; reset input buffer
	lda #$00
	tay
	sta (.bufptr)

	; put input into buffer until return is pressed
.inputloop	
	jsr keyin

	cmp #KEY_RETURN ; return?
	beq .parse

	cmp #KEY_BACKSPACE
	beq .backspace

	cmp #KEY_ESCAPE
	beq .escape


	sta (.bufptr),y
	iny

-	jsr .terminate
	jsr krn_chrout

	; prevent overflow of input buffer 
	cpy #BUF_SIZE
	beq mainloop

	
	bra .inputloop

.backspace
	cpy #$00
	beq .inputloop

	dey

	bra -

.escape 
	jsr getkey
; 	cmp #KEY_ESCAPE_CRSR_UP
; 	bne +

; 	jsr .decbufptr
; 	bra ++

; +	cmp #KEY_ESCAPE_CRSR_DOWN
; 	bne +

; 	jsr .incbufptr
; ++
	jsr .printbuf

; +	
	bra .inputloop

.terminate
	pha
	lda #$00
	sta (.bufptr),y
	pla
	rts

.parse
	+copyPointer .bufptr, cmdptr

	; find begin of command word
-	lda (cmdptr)	; skip non alphanumeric stuff	
	bne +
	jmp mainloop
+
	cmp #$20
	bne +
	inc cmdptr
	bra -
+
	+copyPointer cmdptr, paramptr

	; find begin of parameter (everything behind the command word, separated by space)
	; first, fast forward until space or abort if null (no parameters then)
-	lda (paramptr)
	beq ++
	cmp #$20
	beq +
	inc paramptr
	bra -
+	
	; space found.. fast forward until non space or null
-	lda (paramptr)
	beq ++
	cmp #$20
	bne +
	inc paramptr
	bra -
+
++

	
; 	; jsr .incbufptr
; 	lda #BUF_SIZE
; 	clc
; 	adc .bufptr
; 	sta .bufptr
; 	bcc +
; 	inc .bufptr+1
; +

; 	+copyPointer .bufptr, .bufhwm
	
; 	; last buffer slot? no increasing
; 	lda .bufptr+1
; 	cmp #>.endbuf
; 	bne +
; 	lda .bufptr
; 	cmp #<.endbuf
; 	bne +

	+SetVector .buf, .bufptr
+

	jsr .terminate
	


	; compare 	
	ldx #$00
-	ldy #$00
--	lda (cmdptr),y

	; if not, there is a terminating null
	; cmp #$00
	bne +

	cmp .cmdlist,x
	beq .cmdfound

	; command string in buffer is terminated with $20 if there are cmd line arguments
	
+	
	cmp #$20
	bne +
	; beq .cmdfound
	cmp .cmdlist,x
	bne .cmdfound
+
	; make lowercase
	ora #$20

	cmp .cmdlist,x
	bne +	; difference. this isnt the command were looking for

	iny
	inx

	bra --

	; next cmdlist entry
+
--	inx
	lda .cmdlist,x
	bne --
	inx
	inx
	inx

	lda .cmdlist,x
	cmp #$ff
	beq .unknown
	bra -

.cmdfound
	inx

	jmp (.cmdlist,x) ; 65c02 FTW!!	

.unknown
	lda (.bufptr)
	beq +

	; +SetVector .buf, paramptr
	+ShellPrintString .crlf
	jmp .run

+	jmp mainloop

.printbuf:
	ldy #$01
	sty crs_x
	jsr textui_update_crs_ptr
	
	ldy #$00
-	lda (.bufptr),y
	beq ++
	sta .buf,y
	jsr krn_chrout
	iny
	bra -
++	rts

; .incbufptr:
; 	lda .bufptr+1
; 	cmp .bufhwm+1
; 	bne +
; 	lda .bufptr
; 	cmp .bufhwm
; 	bne +

; 	rts
; +

; 	; advance bufptr by BUF_SIZE bytes
; 	lda #BUF_SIZE
; 	clc
; 	adc .bufptr
; 	sta .bufptr
; 	bcc ++
; 	inc .bufptr+1
; ++
; 	; last buffer slot? start from beginning

	
; 	rts

; .decbufptr:
; 	; first buffer slot? do not decrease
; 	lda .bufptr+1
; 	cmp #>.buf
; 	bne +
; 	lda .bufptr
; 	cmp #<.buf
; 	bne + 

; 	rts
; +
; 	; decrease bufptr by BUF_SIZE bytes
; 	lda .bufptr
; 	sec
; 	sbc #BUF_SIZE
; 	sta .bufptr

; 	bcs +
; 	dec .bufptr+1
; +	
; 	rts

.cmdlist
	!text "dir"
	!byte $00
	!word .dir

	!text "ll"
	!byte $00
	!word .dir

	!text "ls"
	!byte $00
	!word .ls

	!text "cd"
	!byte $00	
	!word .cd
	
	!text "upload"
	!byte $00	
	!word .upload

	!text "init"
	!byte $00	
	!word .init

	!text "help"
	!byte $00	
	!word .help

	!text "dump"
	!byte $00	
	!word .dump
	
	; End of list
	!byte $ff



.atoi
	cmp #"9"+1
	bcc + 	; 0-9?
	; must be hex digit
	adc #$08
	and #$0f
	rts

+	sec
	sbc #$30
	rts

.param2fileptr
 	+copyPointer paramptr, filenameptr
 	rts


.dir_show_entry_short
	pha
	jsr .print_filename
	+PrintChar ' '
	+PrintChar ' '
	dec entries
	bne +	
	+ShellPrintString .crlf
	lda #num_ls_entries
	sta entries
+
	pla
	rts

.dir_show_entry
	pha
	jsr .print_filename
	+PrintChar ' '

	ldy #DIR_Attr
	lda (dirptr),y


	ror
	ror
	ror
	bcc +
	lda #'V'
	bra ++
+
	ror
	bcc +
    lda #'S'
    bra ++
+	
	ror
	bcc +
	lda #'D'
	bra ++
+
	lda #'F'	

++	+ShellPrint

	+PrintChar ' '
	ldy #DIR_FileSize + 1 +1
-	dey
	lda (dirptr),y
	+ShellPrintHex
	cpy #DIR_FileSize
	bne -	

	+PrintChar ' '

	; ldy #DIR_FstClusHI+1
	; lda (dirptr),y
	; jsr hexout
	; ldy #DIR_FstClusHI+0
	; lda (dirptr),y
	; jsr hexout

	; ldy #DIR_FstClusLO+1
	; lda (dirptr),y
	; jsr hexout
	; ldy #DIR_FstClusLO+0
	; lda (dirptr),y
	; jsr hexout

	; day
	ldy #DIR_WrtDate 
	lda (dirptr),y
	and #%00011111
	jsr .decoutz
	
	; +PrintChar '.'
	; month
	iny
	lda (dirptr),y
	lsr
	tax
	dey
	lda (dirptr),y
	ror
	lsr
	lsr
	lsr
	lsr
	
	jsr .decoutz
	
	; +PrintChar '.'
	; year
	txa
	clc 
	adc #80   	; add begin of msdos epoch (1980)
	cmp #100	
	bcc +		; greater than 100 (post-2000)
	sec 		; yes, substract 100
	sbc #100
+	jsr .decoutz ; there we go

	

	+PrintChar ' '

	ldy #DIR_WrtTime +1
	lda (dirptr),y
	tax
	lsr
	lsr
	lsr
	
	jsr .decoutz

	+PrintChar ':'

	txa
	and #%00000111
	sta tmp1
	dey
	lda (dirptr),y
	
	!for .i,0,4 {
		lsr tmp1
		ror 
	}	


	jsr .decoutz


	; Bits 11–15: Hours, valid value range 0–23 inclusive.
	
	+ShellPrintString .crlf
+++	pla
	rts	

.print_filename
	ldy #DIR_Name
-	lda (dirptr),y
	+ShellPrint	
	iny
	cpy #$0b
	bne -
	rts


!src <util.a>
.errmsg
		; jsr hexout

		+errMsgEntry fat_bad_block_signature, .fat_err_signature
		+errMsgEntry fat_invalid_partition_type, .fat_err_partition
		+errMsgEntry fat_invalid_sector_size, .fat_err_bad_sect_size
		+errMsgEntry fat_invalid_num_fats, .fat_err_bad_sect_size		
		+errMsgEntry fat_open_error, .fat_err_open
		+errMsgEntry fat_too_many_files, .fat_err_too_many_open
		+errMsgEntry fat_file_not_found, .fat_err_no_such_file
		+errMsgEntry fat_file_not_open, .fat_err_file_not_open

		jsr strout
		
		rts


.hellotxt		!text "SteckShell 0.11 ",$00
.helptxt1		
				!text $0a,$0d,"ll/ls       - directory (long/short)"
				!text $0a,$0d,"cd <name>   - change directory"
				; !text $0a,$0d,"run <name>  - run program"		
				!text $0a,$0d,"dump <addr> <addr> - dump memory"
				!byte $00

.crlf			!byte $0a,$0d,$00
.prompt			!text $0a,$0d,">",$00

.txt_msg_param_error	!text $0a,$0d,"parameter error",$0a,$0d,$00
.txt_msg_sd_init_error	!text $0a,$0d,"sdcard init error",$0a,$0d,$00
.txt_cd 				!text "cd ok",$00

.fat_err_signature 		!text "bad block signature", $00
.fat_err_partition 		!text "invalid partition type", $00
.fat_err_bad_sect_size 	!text "sector size unsupported", $00
.fat_err_open			!text "open error",$00
.fat_err_no_such_file	!text "no such file or directory",$00
.fat_err_file_not_open	!text "file not open error",$00
.fat_err_too_many_open	!text "too many open files",$00
.exec_extension			!text ".bin",$00

.filename !text "            ",$00
.dir_entry
	jmp (entryvec)

.ls
	lda #num_ls_entries
	sta entries
	+SetVector .dir_show_entry_short, entryvec

	bra +
.dir
	+SetVector .dir_show_entry, entryvec
+	+ShellPrintString .crlf
	+SetVector .pattern, filenameptr

	lda (paramptr)
	beq +
	+copyPointer paramptr, filenameptr
+

	jsr find_first
	bcs ++
	bra +
	; jsr .dir_show_entry
-
	jsr find_next
	bcc +
++	
	lda (dirptr)
	cmp #$e5
	beq -

	ldy #DIR_Attr
	lda (dirptr),y

	bit dir_attrib_mask ; Hidden attribute set, skip
	bne -


	jsr .dir_entry

	jsr getkey
	cmp #$03 ; CTRL-C?
	beq +
	bra -
+
	jmp mainloop


.cd
	; is it a slash?
	ldy #$00
	lda (paramptr),y
	cmp #'/'
	bne +
	iny
	lda (paramptr),y
	bne +
	
	; its a slash, nothing else. cd to /
	jsr open_rootdir
	jmp mainloop

+	; not a slash. cd to whatever

	jsr .param2fileptr
	
	+ShellPrintString .crlf
	jsr open

	lda errno
	beq +
	jsr .errmsg
	; +ShellPrintString .txt_cd
	jmp mainloop
+
	+ShellPrintString .txt_cd
-	jmp mainloop 


.run

	lda #<.filename
	sta filenameptr
	lda #>.filename
	sta filenameptr+1
	
	ldy #$00
-	lda (cmdptr),y
	beq ++
	sta .filename,y
	cmp #' '
	beq ++
	
	cmp #'.'
	beq + ; has extension
	iny
	bra -	

++	
	ldx #$00
-	lda .exec_extension,x
	sta .filename,y
	iny
	inx
	cpx #$05
	bne -

	

+	
	; +copyPointer cmdptr, filenameptr
	jsr .readfile		
	
	lda errno
	beq + 
	jmp mainloop
+

	; ldx #$ff
	; txs

	; sei
	; jmp  steckos_start

	jmp steckos_start

; .load
; 	jsr .param2fileptr
; 	jsr .readfile		
	
; 	jmp mainloop


.dumpvec 		= tmp2
.dumpvec_end   	= .dumpvec
.dumpvec_start 	= .dumpvec+2

.dump
	; stz .dumpvec
	stz .dumpvec+1
	stz .dumpvec+2
	stz .dumpvec+3

	ldy #$00
	ldx #$03
-	lda (paramptr),y
	beq +

	jsr .atoi
	asl
	asl
	asl
	asl
	sta .dumpvec,x

	iny
	lda (paramptr),y
	beq +
	jsr .atoi
	ora .dumpvec,x
	sta .dumpvec,x
	dex
	iny
	cpy #$04
	bne -

	iny
	bra -

+	cpy #$00
	bne ++

	+ShellPrintString .txt_msg_param_error
	bra +

++	

-	
	+ShellPrintString .crlf
	lda .dumpvec_start+1
	jsr hexout
	lda .dumpvec_start
	jsr hexout
	lda #':'
	jsr krn_chrout
	lda #' '
	jsr krn_chrout

	ldy #$00
--	lda (.dumpvec_start),y
	jsr hexout 
	lda #' '
	jsr krn_chrout
	iny
	cpy #$08
	bne --

	lda #' '
	jsr krn_chrout
	
	ldy #$00
--	lda (.dumpvec_start),y
	cmp #$19
	bcs ++
	lda #'.'
++	jsr krn_chrout
	iny
	cpy #$08
	bne --

	lda .dumpvec_start+1
	cmp .dumpvec_end+1
	bne ++
	lda .dumpvec_start
	cmp .dumpvec_end
	beq +
	bcs +

++
	jsr getkey
	cmp #$03
	beq +
	clc
	lda .dumpvec_start

	adc #$08
	sta .dumpvec_start
	lda .dumpvec_start+1
	adc #$00
	sta .dumpvec_start+1
	bra -

+	jmp mainloop

	
.readfile

	jsr open
 	stx tmp5
 
	lda errno
	bne +

	
	+SetVector steckos_start, sd_blkptr
	jsr read

	ldx tmp5
	jsr close
	rts

+	
	+ShellPrintString .crlf
	ldy #$00
-	lda (filenameptr),y
	beq +
	jsr krn_chrout
	iny
	bra -
+
	lda #':'
	jsr krn_chrout	
	lda #' '
	jsr krn_chrout

	lda errno
	jsr .errmsg
	; plp	
	rts


.upload
	sei
	jsr upload
    cli
	; jump to new code
	jmp (.startaddr)	

.hello
	+ShellPrintString .crlf
	+ShellPrintString .hellotxt
	; +ShellPrintString .helptxt
	jmp mainloop

.help
	+ShellPrintString .crlf
	; +ShellPrintString .hellotxt
	+ShellPrintString .helptxt1
	jmp mainloop

init_textui:
	jsr	display_off			;restore textui
	jsr	textui_init
	jsr	textui_enable
	rts

.pattern	!text "*.*",$00
; .return
; 	jsr init_textui
; 	jmp mainloop
