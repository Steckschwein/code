GAL22V10    ; first line : used GAL
8Bit Dekoder    ; second line: any text (max. 8 char.)

; PIN assignment
;
G1       ; A15 of 6502 (Pin 25)
A2       ; A14 of 6502 (Pin 24)
A1       ; A13 of 6502 (Pin 23)
A0       ; A12 of 6502 (Pin 22)
B0       ; A08 of 6502 (Pin 20)
B1       ; A09 of 6502 (Pin 19)
B2       ; A10 of 6502 (Pin 18)
B3       ; A11 of 6502 (Pin 17)
RW       ; RW of 6502 (Pin 34)
CLK1		;	8Mhz	
CLK2		;	4Mhz
GND
;
ROMOFF	; Remove ROM from Adress space if low, make unterlying RAM readable
CSROM    ;CS signal for ROM at $e000-$ffff 
NC			;
NC			;
CSHIRAM  ;CS for ram between  $8000-$cfff
CSUART   ; CS for UART
CSVIA    ;6522 VIA    at $d100
ELCD     ;LCD-Display at $d200
VDPCSR   ;Read VDP at $d400
VDPCSW   ;Write VDP at $d500
PHI2T		;
VCC
;
;  boolean expressions
;
/CSHIRAM = G1*/A2                       
         + G1*/A1*/A0
         + /RW*G1*A2*A1 				; Writes to $e000-$ffff go to the RAM
         + /ROMOFF*RW*G1*A2*A1			; Reads  to $e000-$ffff go to the RAM, IF ROMOFF is Low
/CSROM   = ROMOFF*RW*G1*A2*A1			; Reads  to $e000-$ffff go to the ROM, IF ROMOFF is High

/CSVIA   = G1 * A2*/A1*A0 * /B3*/B2*/B1*B0		; $d100

ELCD     = CLK2			* G1 * A2*/A1*A0 * /B3*/B2*B1*/B0	; $d200 - LCD-Display at $d200
			+/CLK2*CLK1 	* G1 * A2*/A1*A0 * /B3*/B2*B1*/B0	

; Variante	early CSW/CSR
/VDPCSR  = RW* /CLK2*CLK1 	* G1*A2*/A1*A0  * /B3*B2*/B1*/B0	; $d400 - Video CSR     1101 0100 d400
			+ RW* CLK2*/CLK1 	* G1*A2*/A1*A0  * /B3*B2*/B1*/B0	; $d400 - Video CSR     1101 0100 d400
/VDPCSW  = /RW*/CLK2*CLK1	* G1*A2*/A1*A0  * /B3*B2*/B1*/B0	; $d400 - Video CSW     1101 0100 d400
			 +/RW*CLK2*/CLK1	* G1*A2*/A1*A0  * /B3*B2*/B1*/B0	; $d400 - Video CSW     1101 0100 d400

; Legacy
; /UARTWR  = /RW * PHI2 * G1*A2*/A1*A0 * /B3*/B2*/B1*/B0	; $d000 - UART
; /UARTRD  = RW *  G1*A2*/A1*A0 * /B3*/B2*/B1*/B0			; $d000 - UART
/CSUART  = G1*A2*/A1*A0 * /B3*/B2*/B1*/B0

PHI2T=CLK2
		+/CLK2*CLK1

DESCRIPTION: