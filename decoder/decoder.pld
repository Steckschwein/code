GAL22V10    ; first line : used GAL
8Bit Dekoder    ; second line: any text (max. 8 char.)

; PIN assignment
;
G1       ; A15 of 6502 (Pin 25)
A2       ; A14 of 6502 (Pin 24)
A1       ; A13 of 6502 (Pin 23)
A0       ; A12 of 6502 (Pin 22)
B0       ; A08 of 6502 (Pin 20)
B1       ; A09 of 6502 (Pin 19)
B2       ; A10 of 6502 (Pin 18)
B3       ; A11 of 6502 (Pin 17)
RW       ; RW of 6502 (Pin 34)
PHI2     ; PHI2 of 6502 (Pin 39)
NC
GND
NC 
CSROM    ;CS signal for ROM at $e000-$ffff 
OE
WE       ;with PHI2 synchronized WE
CSHIRAM  ;CS for ram between  $8000-$cfff
CSUART   ;CS for UART at $d000
CSVIA    ;6522 VIA    at $d100
ELCD     ;LCD-Display at $d200
VDPCSR   ;Read VDP at $d400
VDPCSW   ;Write VDP at $d400
NC
VCC
;
;  boolean expressions
;
OE       = /RW                                        ; - output enable (active low, read from adress)   		
/WE      = /RW * PHI2                                 ; - write enable, combined with PHI2 (Pin 39) for synchronisation
/CSHIRAM = G1*/A2                                     ; $8000-$cfff
         + G1*/A1*/A0
/CSROM   = G1*A2*A1					; $e000-$ffff
/CSUART  = G1*A2*/A1*A0 * /B3*/B2*/B1*/B0		; $d000 - UART
/CSVIA   = G1 * A2*/A1*A0 * /B3*/B2*/B1*B0		; $d100
ELCD     = PHI2 * G1 * A2*/A1*A0 * /B3*/B2*B1*/B0	; $d200 - LCD-Display at $d200
/VDPCSR  = /OE  * G1 * A2*/A1*A0 * /B3*B2*/B1*/B0	; $d400 - Video CSR     1101 0100 d400
/VDPCSW  = /RW*G1*A2*/A1*A0  * /B3*B2*/B1*B0		; $d500 - Video CSW     1101 0101 d500

DESCRIPTION:
