*=$1000
	jmp	main
!source "../bios/defs.h.a"
!source "../bios/bios.h.a"
!source "../vdp/t9929.h.a"
!source "../vdp/t99xx.lib.a"

yOffs=tmp0
seed=tmp1
move_on=tmp2
scrollpos=tmp3
text:
!text	"VCFe.CH - 2014"
!fill	255, $20
!text	0
main:
	sei

	+SetVector	starfield_tab, adrl
	jsr	vdp_init_reg

	+SetVector	chars, adrl
	lda	#$00		;setup pattern
	ldy	#$00+$40
	ldx	#$10		;
	jsr	vdp_memcpys

	lda	#Cyan<<4|Black		;setup screen color gfx1
	sta	adrl
	lda	#$00
	ldy	#$20+$40
	ldx	#$20		; $20 possible colors
	jsr	vdp_fills
	
	lda	#$20		;clear screen gfx1
	sta	adrl
	lda	#$00
	ldy	#$18+$40
	ldx	#$03		; $300 chars
	jsr	vdp_fill
	
	jsr	init_sprites
	
	+SetVector	stars_irq,	irqvec
	
	lda	#v_reg1_16k|v_reg1_display_on|v_reg1_int
	ldy	#v_reg1
	jsr	vdp_sreg
	
	ldx		#08
	ldy		#12
	jsr		vdp_xy
	+SetVector	text, adrl
	jsr		vdp_print
	
	cli
	
	stz		move_on
	stz		scrollpos
	
-	lda		move_on
	beq		-
	jsr 	move_stars
	jsr		text_scroll
	
	dec		move_on
	jmp	-
	
	rts

stars_irq:
	+save

	lda	a_vreg
	
	;update sprite tab
	+SetVector	starfield_spritetab, adrl
	lda	#$00
	ldy	#$3c+$40
	ldx	#$20*4
	jsr vdp_memcpys
	
	;update text
	+SetVector	text_scroll_buf, adrl
	lda	#$00
	ldy	#$18+$40
	ldx	#$20
	jsr vdp_memcpys
	
	inc	move_on
	
	+restore
	rti
	
text_scroll:
	ldx	#$00
-	lda	text_scroll_buf+1	,x
	sta	text_scroll_buf		,x
	inx
	cpx	#$20
	cpx	#$1f
	bne	-
	ldx	scrollpos
	lda text,x
	sta text_scroll_buf+$1f
	inc scrollpos
	
	rts
	
move_stars:
	ldx #$00
	ldy #$00
-	lda starfield_spritetab+1	,y
	clc
	adc starfield_speed_tab		,x
	sta starfield_spritetab+1	,y
	iny
	iny
	iny
	iny
	inx
	cpx	#$20
	bne  -
	rts
	
init_sprites:
	lda	#$30
	sta seed
	stz yOffs	
	ldx #$00
-	lda yOffs
	sta starfield_spritetab,y	; y pos
	clc
	adc #$06
	sta yOffs
	jsr rnd
	sta starfield_spritetab+1,y		; x offset
	and	#$07
	ora	#$01
	sta starfield_speed_tab,x		; speed
	and	#$0f
	lda	#White
	;and	#$07
	sta starfield_spritetab+3,y
	lda	#$00
	sta starfield_spritetab+2,y		; pattern
	iny
	iny
	iny
	iny
	inx
	cpx	#$20
	bne -
	rts

starfield_tab:
	!byte	0
	!byte v_reg1_16k
	!byte ($1800 / $400)	; name table - value * $400
	!byte	($2000 / $40)	; color table
	!byte	($0000 / $800) ; pattern table
	!byte	($3c00 / $80)	; sprite attribute table - value * $80 --> offset in VRAM
	!byte	($0000 / $800)	; sprite pattern table - value * $800  --> offset in VRAM
	!byte	Black

rnd:
   lda seed
   beq doEor
   asl
   beq noEor ;if the input was $80, skip the EOR
   bcc noEor
doEor:    
	eor #$1d
noEor:  
	sta seed
	rts
	
chars:
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%...#....
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%........
blank:
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%........
+SpriteLine	%........

starfield_speed_tab:
	!fill 32, 0
starfield_spritetab:;y,x,pattern,attr
	!fill 32*4, 0
text_scroll_buf:
	!fill 32, 0
