;{{{ The MIT License
;
;Copyright (c) 2008-2013 Johan Kotlinski, Mats Andren
;
;Permission is hereby granted, free of charge, to any person obtaining a copy
;of this software and associated documentation files (the "Software"), to deal
;in the Software without restriction, including without limitation the rights
;to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
;copies of the Software, and to permit persons to whom the Software is
;furnished to do so, subject to the following conditions:
;
;The above copyright notice and this permission notice shall be included in
;all copies or substantial portions of the Software.
;
;THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
;IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
;FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
;AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
;LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
;OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
;THE SOFTWARE. }}}

; disk i/o
READST = $ffb7
SETLFS = $ffba
SETNAM = $ffbd
OPEN = $ffc0
CLOSE = $ffc3
CHKIN = $ffc6
CHKOUT = $ffc9
CLRCHN = $ffcc
CHRIN = $ffcf
CHROUT = $ffd2
LOAD = $ffd5
SAVE = $ffd8

; -----

; Active file stream to compile from.
COMPILE_FILENO
    !byte	0

; CLOSEW ( file# -- )
    +BACKLINK
    !byte 6
    !text	"closew"
CLOSEW
    lda LSB,x
    sta TMP
    stx	TMP2
    jsr	CLOSE 
    ldx	TMP
    jsr	CHKOUT
    jsr	_errorchread
    ldx TMP2
    inx
    rts

_errorchread
        LDA #$00      ; no filename
        tax
        tay
        JSR SETNAM
        LDA #$0F      ; file number 15
        LDX $BA       ; last used device number
        BNE +
        LDX #$08      ; default to device 8
+	    LDY #$0F      ; secondary address 15 (error channel)
        JSR SETLFS

        JSR OPEN
        BCS .error    ; if carry set, the file could not be opened

        LDX #$0F      ; filenumber 15
        JSR CHKIN     ; file 15 now used as input

        LDY #$00
.loop   JSR READST    ; read status byte
        BNE geof      ; either EOF or read error
        JSR CHRIN     ; get a byte from file
        JSR CHROUT    ; print byte to screen
        JMP .loop     ; next byte

geof
.error
        ; Accumulator contains BASIC error code

        ; most likely error:
        ; A = $05 (DEVICE NOT PRESENT)

        ; ... error handling for open errors ...

.glose
        LDA #$0F      ; filenumber 15
        JSR CLOSE

        LDX #$00      ; filenumber 0 = keyboard
        JMP CHKIN     ; keyboard now input device again


; LOADB ( filenameptr filenamelen dst -- status ) load binary file
;  - s" base" 7000 loadb #load file to 7000
;  - end of loaded file is available in $ae/$af afterwards
    +BACKLINK
    !byte 5
    !text	"loadb"
LOADB
    txa
    pha

    lda MSB, x		; >destination
    sta load_binary_laddr_hi
    lda LSB, x		; <destination
    sta load_binary_laddr_lo

    lda LSB+1, x		; a filename length
    pha
    ldy MSB+2, x 		; y >basename 
    lda LSB+2, x		; x <basename 
    tax
    pla
    jsr	load_binary
    
    pla
    tax

    inx
    inx
    lda	load_binary_status
    sta	MSB, x
    sta	LSB, x
    beq	+
    txa
    pha
    jsr	_errorchread
    pla
    tax
+
    rts

load_binary_status
    !byte	0

;load_binary_base
;	lda	#basename_end - basename
;	ldx	#<basename
;	ldy	#>basename
;	; fall through...

load_binary
    jsr .disk_io_setnamsetlfs

load_binary_laddr_lo = *+1
    ldx #$ff	;<load_address
load_binary_laddr_hi = *+1
    ldy #$ff	;>load_address
    lda #0		;0 = load to memory (no verify)
    sta	load_binary_status
    jsr LOAD
    bcs .disk_io_error
    ldx #$00      ; filenumber 0 = keyboard
    jmp CHKIN     ; call CHKIN (keyboard now input device again)

.disk_io_setnamsetlfs ;reused by both loadb and saveb
    jsr SETNAM
    lda $ba		;last used device number
    and #3		;Make 0-3 possible numbers
    ora #8		;Transform to 8-B
    tax
    lda #1
    ldy #0		;if load: 0 = load to new address, if save: 0 = dunno, but okay... 
    jmp SETLFS	;End with JMP instead of jsr/rts to save a jsr/rts pair...

.disk_io_error
    ; Accumulator contains BASIC error code
    
    ;... error handling ...
    ldx #$00      ; filenumber 0 = keyboard
    jsr CHKIN     ; call CHKIN (keyboard now input device again)
    lda	#1
    sta	load_binary_status
    rts

; SAVEB (save binary file)
;  - 7000 71ae s" base" saveb #save file from 7000 to 71ae (= the byte AFTER the last byte in the file)
    +BACKLINK
    !byte 5
    !text	"saveb"
SAVEB
    stx tmp_x

    lda	$ae
    pha
    lda	$af
    pha

    lda LSB+3, x		; range begin lo
    sta $c1
    lda MSB+3, x		; range begin hi
    sta $c2
    
    lda LSB+2, x		; range end lo
    sta save_binary_srange_end_lo
    lda MSB+2, x		; range end hi
    sta save_binary_srange_end_hi

    lda LSB, x		; a filename length
    pha
    ldy MSB+1, x 		; y basename hi
    lda LSB+1, x		; x basename lo
    tax
    pla

    jsr .disk_io_setnamsetlfs
    
    ;This should point to the byte AFTER the last byte in the file.
save_binary_srange_end_lo = *+1
    ldx #$ff	;load_address lo
save_binary_srange_end_hi = *+1
    ldy #$ff	;load_address hi
    lda #$c1	;tell routine that start address is located in $c1/$c2
    jsr SAVE
    bcs .disk_io_error

    pla
    sta	$af
    pla
    sta	$ae

    ldx tmp_x
    inx
    inx
    inx
    inx
    rts

; OPENW ( strptr strlen file# ) open file for writing
    +BACKLINK
    !byte 5
    !text	"openw"
OPENW
    lda LSB,x
    sta TMP ; fileno
    stx	TMP2

    lda	LSB+1, x
    ldy	MSB+2, x
    pha
    lda	LSB+2, x
    tax
    pla
    
    jsr	SETNAM
    lda	TMP ; file number
    ldx	$ba ; last used device#
    tay ; secondary address
    jsr	SETLFS
    jsr	OPEN
    bcc	+
    jsr .close
    jmp ++
+
    ldx	TMP ; file number
    jsr	CHKOUT
++
    ldx	TMP2
    inx
    inx
    inx
    rts

.close
    lda $b8 ; current file
    jsr CLOSE
    jmp CLRCHN

; LOAD_AND_COMPILE (load and compile source file)
    +BACKLINK
    !byte 4
    !text	"load"
LOAD_AND_COMPILE
    txa
    pha

    lda	LSB, x
    ldy	MSB+1, x
    pha
    lda	LSB+1, x
    tax
    pla

    ; open file
    ; a = filename length
    ; y = basename hi
    ; x = basename lo
    jsr	SETNAM
    inc	COMPILE_FILENO
    lda	COMPILE_FILENO ; file number
    ldx	$ba ; last used device#
    tay
    iny ; secondary address
    jsr	SETLFS
    jsr	OPEN
    bcc	+
    dec COMPILE_FILENO
    jsr .close
    jmp ++
+
    ldx	COMPILE_FILENO ; file number
    jsr	CHKIN

    ; Skips load address. It is tempting to keep the source
    ; code as .SEQ files instead of .PRG to avoid this step.
    ; However, the advantage with .PRG is that loading/saving
    ; files from text editor can be dramatically speeded up
    ; by fast loader cartridges such as Retro Replay.
    JSR CHRIN     ; get a byte from file
    JSR CHRIN     ; get a byte from file
++
    pla
    tax
    inx
    inx
    rts
