!ifdef macro_h_a !eof
macro_h_a = 1

!macro SetVector .word, .addr {
        lda #<.word
        sta .addr
        lda #>.word
        sta .addr+1
}

!macro PrintChar char {
        lda #char
        jsr krn_chrout
}
!macro Println {
    lda #10
    jsr krn_chrout
    lda #13
    jsr krn_chrout
}

!macro PrintString .word {
		+SetVector .word, msgptr
        jsr strout
}
!macro String .addr {
		phy
    	ldy #$00
-		lda .addr,y
		beq +
		jsr chrout
		iny
		bne -
+		ply
}
!macro PrintStringAt .word,.x,.y {
 	lda #.x
	sta	crs_x
	lda	#.y
	sta crs_y
	+PrintString .word
 }

!macro save{
	pha
	phy
	phx
}
!macro restore{
	plx
	ply
	pla
}

!macro Copy .src, .trgt, .len {
	ldx #.len
-	lda .src,x
	sta .trgt,x
	dex
	bpl -
}
!macro copyPointer .fromptr, .toptr {
	!for i,0,1 {
		lda .fromptr+i
		sta .toptr	+i	
	}
}

!macro toUpper {

	cmp #$60 ; Is lowercase?
	bcc +
	and	#$DF
+
}

!macro toLower {
	ora #$20
}

!macro inc16 .word {
	inc .word
	bne +
	inc .word+1
+
}

!macro dec16 .word {
	lda .word
	bne +
	dec .word+1
+	dec .word
}

!macro debug .x {
	lda #.x
	jsr chrout
} 
!macro debug32 .x {
!for i,3,0 {
	lda .x+i
	jsr hexout
}
}

!macro ShellPrint {
	jsr krn_chrout
}

!macro ShellPrintHex {
	jsr	hexout
}
; !macro ShellPrintDec {
; 	; leading zero hack
; 	cmp #10
; 	bcs +
; 	pha
; 	+PrintChar '0'
; 	pla
; +	
; 	jsr decout
; }

!macro ShellPrintString .word {
	+SetVector .word,	msgptr
	jsr strout
}

