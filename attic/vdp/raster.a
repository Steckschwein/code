*=$1000
    jmp main
!src <defs.h.a>
!src <macro.h.a>
!src <t99xx.lib.a>

!source "../steckos/asminc/via.inc"

.safeirq !word 0

main
    sei
    
    lda #1<<0	;bios off
	sta $0230
;    +SetVector  $fffe, .safeirq	
    +SetVector  .raster_irq, $fffe
    
    lda #Cyan<<4
    jsr vdp_mode_gfx1
    
    lda #%11000000
	sta via1ier             ; enable VIA1 T1 interrupts
    
    lda #03
    sta tmp1
    
    cli
-   
    bra -

.raster_irq
    pha
    
    bit a_vreg
    bpl +
    jsr .via_timer_blank    ;bottom border, start first via timeout until start of active display area after 4300µs
    lda #00                 ;32 bars
    sta tmp0
    lda #Black
    bra +++
    
+	bit via1ifr		; Interrupt from VIA?
	bpl ++
	bit via1t1cl	; Acknowledge timer interrupt
    lda tmp1
    beq ++

rmWaitTweak=3
rmWait=rmWaitTweak+(57*1000/125)
    lda #<rmWait   ; 63,695µs per line -> 63000
	sta via1t1cl            ; set low byte of count
	lda #>rmWait
	sta via1t1ch            ; set high byte of count
    ldx tmp0
    lda .raster_bar_colors,x
    inx
    cpx #13
    bne +
    dec tmp1
    ldx #0
+   stx tmp0
    bra +++
    
++ 
    lda #14
    sta tmp1
	lda .raster_bar_colors
	pha
	ldx #0
.scroll
	lda .raster_bar_colors+1, x
	sta .raster_bar_colors, x
	inx 
	cpx #12
	bne .scroll
	pla
	sta .raster_bar_colors, x
    lda #Black
+++
    jsr vdp_bgcolor
    
    ;lda #Black
	;jsr vdp_bgcolor
    
    pla
    rti
    
tweak=$659e
.via_timer_blank
    ; 4300µs --> 8Mhz/125ns --> 4.300.000ns / 125ns =  34400cl
    blank_counter=tweak+(4300*1000/125) ;125ns/8Mhz
	lda #<blank_counter
	sta via1t1cl            ; set low byte of count
	lda #>blank_counter
	sta via1t1ch            ; set high byte of count
    rts
    
.raster_bar_colors
	!byte Magenta
	!byte Dark_Red
	!byte	Medium_Red
	!byte	Light_Red
	!byte	Dark_Yellow
	!byte	Light_Yellow
	!byte	White
	!byte	Light_Yellow
	!byte	Dark_Yellow
	!byte	Light_Red
	!byte	Medium_Red
	!byte Dark_Red
	!byte Magenta
.raster_bar_colors_e