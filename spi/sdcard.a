; sdcard pin assignment:
; SD   | Signal 		| VIA Port
;   1  |    /CS 		|          PB1
;   2  | CMD/DI (mosi)		|          CB2
;   5  |    CLK 		|	   PB0/CB1
;   7  | DAT/DO 		|          PB7

*=$1000
!src "../bios/defs.h.a"
!src "../bios/bios.h.a"

!macro SDCommand .cmd, .numres {
	ldx #$00
-	lda .cmd,x
	
	jsr spi_rw_byte
	
	inx
	cpx #$06
	bne -

	jsr spi_r_byte		; byte lesen

	ldx #$00
-		
	jsr spi_r_byte		; byte lesen

+	sta sdcmdres,x		; 
 	inx
 	cpx #.numres
 	bne -
}


!macro PrintSDRes .numres { 
	pha
	phx

	lda #'['
	jsr lcdprint
	ldx #$00
-	lda sdcmdres, x
	jsr lcdhex
	inx
	cpx #.numres
	bne -

	lda #']'
	jsr lcdprint

	pla
	plx
}




; outb 	= $f0
; inb		= $f1
;sdcmdres	= $e0

	jsr lcdclear
	sei

	; SR shift in, External clock on CB1
	; lda #%00001100
	; sta via1acr

	; lda #$ff
	; sta via1ddrb

	; lda #%01111110
	; sta via1portb
init
	ldx #80

	lda #%11111110
	sta via1portb
	tay
	iny
-	sty via1portb
	sta via1portb
	dex

	bne -


	; set CS line to LOW
	lda #%11111101
	and via1portb	
	sta via1portb
	

	; send CMD0 - init SD card to SPI mode
	+SDCommand cmd0, $01
	
	lda sdcmdres
	cmp #$01
	beq +
	
	+PrintString nocard	
	jmp init

+	+PrintString cardok	

	+SDCommand cmd8, $05
	+PrintSDRes $05


	lda #$40
	sta acmd41+1

	ldx #$00

	
initloop	
	phx
	+SDCommand cmd55, $01
	plx
	+SDCommand acmd41, $01
	

	lda sdcmdres
	cmp #$00
	beq initok
	inx
	cpx #$ff
	bne initloop
	
	+PrintString initfail
-	jmp -

initok
	+PrintString initdone

	+SDCommand cmd58, $05
;	+PrintSDRes $05
	

	+SDCommand cmd16, $01
;	+PrintSDRes $01


sd_read_block
	+SDCommand cmd17, $01
	+PrintSDRes $01

	; Wait for data block to become ready
-	jsr spi_r_byte
	cmp #$fe
	bne -

	ldx #$00
-
	jsr spi_r_byte
	sta $2000,x
	inx
	bne -

	ldx #$00
-
	jsr spi_r_byte
	sta $2100,x
	inx
	bne -

	
	jsr spi_r_byte
	jsr spi_r_byte

	lda #'X'
	jsr lcdprint


	; set CS line to HI
	lda #%01111110
	ora via1portb	
	sta via1portb

	jmp upload

; cmd0 reset card to IDLE state
cmd0	!byte %01000000, %00000000, %00000000, %00000000, %00000000, %10010101
cmd1	!byte %01000001, %00000000, %00000000, %00000000, %00000000, %00000001
								      
; cmd8 Sends SD Memory Card interface condition (SD 2.0)
cmd8 	!byte %01001000, %00000000, %00000000, %00000001, %10101010, $87
;cmd8 	!byte %01001000, %00000000, %00000000, %00000001, %10101010, %10010101
; cmd58 Sends SD Memory Card interface condition
cmd58	!byte %01111010, %00000000, %00000000, %00000000, %00000000, %01110101
; cmd55
cmd55	!byte $77, $00, $00, $00, $00, $01
; acmd41
acmd41  !byte $69, $00, $00, $00, $00, $01
; cmd16 SET_BLOCKLEN
cmd16	!byte $50, $00, $00, $02, $00, $01
cmd17	!byte $51, $00, $00, $00, $00, $01
cmd24	!byte $58, $00, $00, $00, $00, $01

cardok	!text "SD Card Found!", $00
nocard	!text "No SD Card!", $00
initdone	!text "Init OK!", $00
initfail	!text "Init Failed!", $00
