*= $4000

text_mode_40 = 1
num_ls_entries = $03

!src <defs.h.a>
!src <bios.h.a>
!src <via.h.a>
; !src <fat32.h.a>
; !src <params.h.a>
; !src <errors.h.a>

!address {
.key = $10
}


kern_init
	sei
	
	stz .key	
	
	lda #<count
	sta via1t1cl            ; set low byte of count
	lda #>count
	sta via1t1ch            ; set high byte of count

	lda #%11000000
	sta via1ier             ; enable VIA1 T1 interrupts

	lda #%01000000
	sta via1acr             ; T1 continuous, PB7 disabled

	+SetVector .kern_irq, irqvec


	; read from keyboard buffer until empty to filter crap from kbd init
-	lda #%01111010
	sta via1portb

	TAX             ; aufheben
    ORA #$01
    !for .i,0,7 {
		STA via1portb ; Takt An 1
		STX via1portb ; Takt aus  
    }
       
	ldx #%11111110
	stx via1portb

	lda via1sr
	cmp #$00
	bne -

	jsr	.textui_init0

	+SetVector _textui_chrout, outvec

	cli
	+SetVector .kernel_version, msgptr
	jsr strout
; -	jsr .keyin
; 	jsr chrout 
; 	jmp -

	jmp $5000	

.keyin
-	lda .key 		; wait for .key to become != 00
	beq -
	stz .key 		; reset .key to signal isr to fetch next key
	rts


; system interrupt handler
; handle keyboard input and text screen refresh
.kern_irq
	+save

	;via
	bit via1ifr		; Interrupt from VIA?
	bpl +			; no? get out

	bit via1t1cl	; Acknowledge timer interrupt

	lda .key		; already char in buffer? do nothing
	bne +

	; jsr getkey
	lda via1portb
	and #%01111110
	cmp #$7e
	bne +

	lda #%01111010
	; and via1portb
	sta via1portb

	TAX             ; aufheben
    ORA #$01
    !for .i,0,7 {
		STA via1portb ; Takt An 1
		STX via1portb ; Takt aus  
    }
       
	ldx #%11111110
	stx via1portb

	lda via1sr
	cmp #$00
	beq +
	sta .key

+
	bit	a_vreg
	bpl +	   ; VDP IRQ flag set?

	jsr	.textui_update_screen
+
	+restore
	rti

!src "../shell/shell.textui.a"
!src "../shell/shell.gfxui.a"

; "kernel" jumptable
krn_keyin	jmp .keyin

; strings
.kernel_version 	!text "SteckOS Kernel 0.1",$0d,$0a,$00


!src "../shell/shell.a"
